apply plugin: 'drwp-common-java8'
apply plugin: 'drwp-lib'
apply plugin: 'drwp-release'


repositories {
    maven { url "${artifactory_url}/libs-beta" }  
    maven { url "${artifactory_url}/drwp-repository-gateway-artifactory-cache" } 
}

def distBase = "${buildDir}/dist"
def opensourceDistDir = "${distBase}/payment-api"

dependencies {
    testCompile(group: 'com.google.code.gson', name: 'gson', version: '2.8.0')
}

sourceSets {
    test {
        java {
            srcDir('src/test/java')
            //stuff is here since they should not be included in source distibution but are needed for tests
            srcDir('src/main/future')
            srcDir('src/test/future')
        }
    }
}

tasks.eclipse.dependsOn cleanEclipse

task includeLibsInDistro(type: Copy) {
    from (libsDir) {
        include '*.jar'
    }
    into "${opensourceDistDir}/libs"
}

task includeSourceFilesInDistro(type: Copy) {
    dependsOn sourceJar
    from sourceSets.main.allSource
    into "${opensourceDistDir}/src"
}

task includeJavadocFilesInDistro(type: Copy) {
    dependsOn javadocJar
    from javadoc.outputs
    into "${opensourceDistDir}/javadoc"
}

task processOpenSourceFiles(dependsOn: ['includeLibsInDistro', 'includeSourceFilesInDistro', 'includeJavadocFilesInDistro'])

task wrapper(type: Wrapper) {
    distributionUrl="${artifactory_url}/third-party-apps/bin/gradle/${wrapperVersion}/gradle-${wrapperVersion}.zip"
}